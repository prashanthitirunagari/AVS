' #####################################################################################################
'	LIBRARY NAME		: LibExcelFunctions
'
'	DESCRIPTION			: This library contains excel funtions
'
'	INCLUDED FUNCTIONS	:
'		Public Function Emulator_diaryRun()
'		Private Function dairyRun()
'		Private Function SelectDairy()
'		Private Function ExecuteDairy()
'       Private Function FindSCDiaryandRun()
'		Private Function InitializeEmulator()
'		Private Function ExitEmulator()
'      Public Function StringWait()

'''#####################################################################################################
Option Explicit
''#####################################################################################################

'#####################################################################################################
'#					 P U B L I C   V A R I A B L E   D E C L A R A T I O N
'#####################################################################################################
'		(none)

'#####################################################################################################
'#					 P R I V A T E   V A R I A B L E   D E C L A R A T I O N
'#####################################################################################################
Private tmpRes
Private Quick3270, Session, Screen
Private EnvString, ServerIP, DRString
Private ResMessage, eMessage, rc
'#####################################################################################################

'################################################################################################################################
'	FUNCTION NAME		: Emulator_diaryRun()

'	DESCRIPTION			: Public Function which invokes emulator and runs the dairy based on the parameters provided..

'	PARAMETERS			: TCase - Test Case Name
'												App --- Application ............ valid values "APS"
'												Region --- Region ............. valid values "IST2","RETRO2","QA"
'												UserID - UserID of the mainframes screen
'												Passwd-- Password
'												PolicyNum -- Policy Number
'												diaryRunString -- Valid Values are ------ "Schedule","Renewal","SurCharge" ......... can give multiple options with ";" as separator

'	DEPENDENCIES		: Execution machine should have Quick3270 Application installed.

' 	RETURN VALUE(S): 
'		micPass	 		: On successful execution of the function
'		micFail	 		: On unsuccessful execution of the function

'	ERRORS				: (None)

'	AUTHOR				: Ramu Gurrapu

'	ORIGINAL DATE		: Sep 18, 2008
'-----------------------------------------------------------------------------------------------------------------------
'			R E V I S I O N    H I S T O R Y
'-----------------------------------------------------------------------------------------------------------------------
'	REVISED DATE	: 	REVISED BY	 :	CHANGE DESCRIPTION
'-----------------------------------------------------------------------------------------------------------------------

Public Function Emulator_diaryRun(ByVal TCase, ByVal App, ByVal Region, ByVal UserID, ByVal PassWD, ByVal PolicyNum, ByVal diaryRunString)
    
	On Error Resume Next 

    Dim DBKeyString, FinalMsg
    Dim tmpIter, tmpWait


    'Reporting 
    rc = ReportStep(1,"DB2 Dairy Run","","")
    
    rc = InitializeEmulator (App, Region)
	If rc <> micPass Or IsObject(Quick3270)=False Then 
		HandleRc micFail
		Emulator_diaryRun = micFail
		ReportStep 2, diaryRunString & " Wait For the Screen","Expected Screen Should be Displayed.","Sync Error : Expected Screen Not Displayed." & eMessage
		Exit Function
	End If
    
    'Entering String SWITH and Passing UserID and PassWord
    With Screen
        .SendKeys (EnvString)
        .SendKeys ("<Enter>")

				rc = StringWait ("User", 8, 9, "Step : Login Screen - After SWITCH") 'Waits for USER ID Screen
				If rc <> micPass Or  IsObject(Quick3270)=False Then 
					HandleRc rc
					Emulator_diaryRun = rc
					ReportStep 2, diaryRunString & " Wait For the Screen","Expected Screen Should be Displayed.","Sync Error : Expected Screen Not Displayed." & eMessage
					Exit Function
				End If
				 
             .Moveto 8, 26
        .SendKeys (UserID)
    End With
    
    If Len(Trim(UserID)) < 8 Then Session.Screen.SendKeys ("<Tab>")
        
    'Passing Env Command String & diary Run Command String
    With Screen
        .SendKeys (PassWD)
        .SendKeys ("<Enter>")

            rc = StringWait ("Welcome", 3, 9, "Current Step: Connected to Enviroment : Waiting for Next Screen")            'Waits for Welcome Screen"
			 If rc <> micPass Or  IsObject(Quick3270)=False Then 
					HandleRc rc
					Emulator_diaryRun = rc
					ReportStep 2, diaryRunString & " Wait For the Screen","Expected Screen Should be Displayed.","Sync Error : Expected Screen Not Displayed." & eMessage
					Exit Function
			End If
			
        .SendKeys (DRString)
        .SendKeys ("<Enter>")

				rc = StringWait("List", 6, 2, "Current Step: AL22 Entered : Waiting for Policy Number Screen")                'Waits for Policy Number Screen"
				 If rc <> micPass Or  IsObject(Quick3270)=False Then 
					HandleRc rc
					Emulator_diaryRun = rc
					ReportStep 2, diaryRunString & " Wait For the Screen","Expected Screen Should be Displayed.","Sync Error : Expected Screen Not Displayed." & eMessage
					Exit Function
				End If
				
    End With
    
    'Code to Enter the Policy Number after DB2 Key String....
    DBKeyString = Trim(Session.Screen.GetString(12, 8, 10))
    If UCase(DBKeyString) = UCase("DB KEY") Then
        With Screen
            .Moveto 12, 8
            .SendKeys ("<Tab>")
            .SendKeys (PolicyNum)
            .SendKeys ("<Enter>")
        End With
    End If
            
		'code to find out how many daires needs to run...
		diaryRunString = Split(diaryRunString, ";")
		For tmpIter = 0 To UBound(diaryRunString)
			rc = diaryRun(diaryRunString(tmpIter))

			If rc <> micPass Then
				HandleRc rc
				FinalMsg = "Dairy Execution Failed for Diary "& diaryRunString(tmpIter) & "   :::    Error Message:" & eMessage
				ReportStep 2, diaryRunString(tmpIter) &" Diary ","Diary Should Execute Successfully.", FinalMsg
				Emulator_diaryRun = micFail
				ExitEmulator
				Exit Function
			Else
				ReportStep 2, diaryRunString(tmpIter) & " Diary ","Diary Should Execute Successfully.","Dairy Executed Successfully."
				Emulator_diaryRun = micPass
			End If

		Next
    
		ExitEmulator	

End Function
'################################################################################################################################
'	FUNCTION NAME		: diaryRun()

'	DESCRIPTION			: Public Function which invokes emulator and runs the dairy based on the parameters provided..

'	PARAMETERS		   : diaryString

'	DEPENDENCIES		: Execution machine should have Quick3270 Application installed.

' 	RETURN VALUE(S): 
'		micPass	 		: On successful execution of the function
'		micFail	 		: On unsuccessful execution of the function

'	ERRORS				: (None)

'	AUTHOR				: Ramu Gurrapu

'	ORIGINAL DATE		: Sep 18, 2008
'-----------------------------------------------------------------------------------------------------------------------
'			R E V I S I O N    H I S T O R Y
'-----------------------------------------------------------------------------------------------------------------------
'	REVISED DATE	: 	REVISED BY	 :	CHANGE DESCRIPTION
'-----------------------------------------------------------------------------------------------------------------------

Private Function diaryRun(ByVal diaryString)

On Error Resume Next

	Dim stRow, FoundDiary
	Dim diaryCode, rc
    
    rc = StringWait ("Enter", 7, 2, "Waiting at diaries list Screen")               'Waits for diary list screen

	'Error Handling -- If Stringwait fails exit the function 
	If rc <> micPass Or IsObject(Quick3270)=False Then 
		diaryRun = micFail
		Exit Function
	End If
	
    stRow = 11
    FoundDiary = False
    Do While Trim(Screen.GetString(stRow, 5, 4)) <> vbNullString
        diaryCode = Trim(Screen.GetString(stRow, 5, 4))
        
        If UCase(Trim(diaryString)) = Ucase("RENEWAL") And Trim(diaryCode) = "Z40A" Then
			rc = SelectDairy (stRow)
			If rc<>micPass Or IsObject(Quick3270)=False Then
				diaryRun = micFail
				Exit Function
			End If

            rc = ExecuteDairy(diaryString, diaryCode)
			If rc<>micPass Or IsObject(Quick3270)=False Then
				diaryRun = micFail
				Exit Function
			Else
				FoundDiary = True
			End If
            Exit Do
            
        ElseIf Ucase(Trim(diaryString)) = Ucase("Schedule") And Trim(diaryCode) = "Z405" Then
            rc =SelectDairy (stRow)
			 If rc<>micPass Or IsObject(Quick3270)=False Then
				diaryRun = micFail
				Exit Function
			End If

            rc = ExecuteDairy(diaryString, diaryCode)
			If rc<>micPass Or IsObject(Quick3270)=False Then
				diaryRun = micFail
				Exit Function
			Else
				FoundDiary = True
			End If
            Exit Do

        
        ElseIf UCase(Trim(diaryString)) = Ucase("SurCharge") And Trim(diaryCode) = "W40A" Then

            rc = FindSCDiaryandRun (stRow, diaryCode, diaryString)
			If rc<>micPass Or IsObject(Quick3270)=False Then
				diaryRun = micFail
				Exit Function
			Else
				FoundDiary = True
			End If
            Exit Do
            
        End If
    
    stRow = stRow + 1
    Loop
    
    If FoundDiary = False Then 
		diaryRun = micFail
		ExitEmulator
		eMessage = diaryString & " Diary not found in the Diaries List."
   Else
		diaryRun = micPass
    End If

End Function

'################################################################################################################################
'	FUNCTION NAME		: SelectDairy()

'	DESCRIPTION			: Public Function which invokes emulator and runs the dairy based on the parameters provided..

'	PARAMETERS		   : RowtoSelect --- Row in which dairy exists

'	DEPENDENCIES		: Execution machine should have Quick3270 Application installed.

' 	RETURN VALUE(S): 
'		micPass	 		: On successful execution of the function
'		micFail	 		: On unsuccessful execution of the function

'	ERRORS				: (None)

'	AUTHOR				: Ramu Gurrapu

'	ORIGINAL DATE		: Sep 18, 2008
'-----------------------------------------------------------------------------------------------------------------------
'			R E V I S I O N    H I S T O R Y
'-----------------------------------------------------------------------------------------------------------------------
'	REVISED DATE	: 	REVISED BY	 :	CHANGE DESCRIPTION
'-----------------------------------------------------------------------------------------------------------------------

Private Function SelectDairy(ByVal RowtoSelect)

    With Screen
        .MoveTo RowtoSelect, 2
        .SendKeys ("X")
        .SendKeys ("<Enter>")
        rc = StringWait ("Process", 7, 2, "Current Step : Diary Selected : Waiting for Next Screen")

		If rc <> micPass Or IsObject(Quick3270)=False Then 
			SelectDairy = micFail
			Exit Function
		Else
			SelectDairy = micPass
		End If   
		
     End With

End Function
'################################################################################################################################
'	FUNCTION NAME		: ExecuteDairy()

'	DESCRIPTION			: Finds surcharge dairy and executes if it found

'	PARAMETERS         :  DiaryCode - dairy code of the dairy

'	DEPENDENCIES		: Execution machine should have Quick3270 Application installed.

' 	RETURN VALUE(S): 
'		micPass	 		: On successful execution of the function
'		micFail	 		: On unsuccessful execution of the function

'	ERRORS				: (None)

'	AUTHOR				: Ramu Gurrapu

'	ORIGINAL DATE		: Sep 19, 2008
'-----------------------------------------------------------------------------------------------------------------------
'			R E V I S I O N    H I S T O R Y
'-----------------------------------------------------------------------------------------------------------------------
'	REVISED DATE	: 	REVISED BY	 :	CHANGE DESCRIPTION
'-----------------------------------------------------------------------------------------------------------------------

Private Function ExecuteDairy(ByVal DiaryStr,ByVal DairyCode)

    Dim ResString, ExpString, ErrString
    
    ExpString = "Transaction " & Trim(DairyCode) & " was started at terminal"
    
    With Screen
        
        rc = StringWait ("Process", 7, 2, "Current Step : Diary Selected : Waiting for Next Screen")
		If rc <> micPass Or IsObject(Quick3270)=False Then 
			ExecuteDairy = micFail
            Exit Function
		End If  
		
        .MoveTo 7, 22
        .SendKeys ("S")
        .SendKeys ("<Enter>")

		rc = StringWait ("Transaction", 22, 19, "Current Step: Diary Ran. Waiting for Next Screen")
        If rc <> micPass Or IsObject(Quick3270)=False Then 
			ExecuteDairy = micFail
			Exit Function
		End If  
		
        ResString = Trim(Screen.GetString(22, 2, 90))

		If InStr(1, ResString, ExpString) <> 0 Then

			.SendKeys ("<Enter>")
			rc = Screen.WaitForCursor(7, 22)

			If rc <> True Then
				ExecuteDairy = micFail
                Exit Function
			End If

			ErrString = Trim(Screen.GetString(18,22,55))

			If ErrString <> vbNullString Then
				ExecuteDairy = micFail
				eMessage = ErrString
				Exit Function
			End If
			
			.SendKeys "<Pf3>"                         																									  		   'Dairy Run SuccessFully. Go back to the dairy screen
			rc = StringWait ("Enter", 7, 2, "Current Step : Diary Ran : Waiting for Next Screen")              'Waits for diary list screen

			If rc <> micPass Or IsObject(Quick3270)=False Then 
				ExecuteDairy = micFail
				Exit Function
			Else
				ExecuteDairy = micPass
			End If  			
			
        End If
        
    End With
            
End Function


'################################################################################################################################
'	FUNCTION NAME		: FindSCDiaryandRun()

'	DESCRIPTION			: Finds surcharge dairy and executes if it found

'	PARAMETERS		   : stRow --- Start Row of the dairy list
'											  Dcode - dairy code of the surcharge

'	DEPENDENCIES		: Execution machine should have Quick3270 Application installed.

' 	RETURN VALUE(S): 
'		micPass	 		: On successful execution of the function
'		micFail	 		: On unsuccessful execution of the function

'	ERRORS				: (None)

'	AUTHOR				: Ramu Gurrapu

'	ORIGINAL DATE		: Sep 19, 2008
'-----------------------------------------------------------------------------------------------------------------------
'			R E V I S I O N    H I S T O R Y
'-----------------------------------------------------------------------------------------------------------------------
'	REVISED DATE	: 	REVISED BY	 :	CHANGE DESCRIPTION
'-----------------------------------------------------------------------------------------------------------------------

Private Function FindSCDiaryandRun(ByVal stRow, ByVal DCode, ByVal diaryString)

	On Error Resume Next

    Dim SurChargeString, SCDairy
    
    stRow = 11
    SCDairy = True
    Do While Trim(Screen.GetString(stRow, 5, 4)) <> vbNullString And Trim(Screen.GetString(stRow, 5, 4)) = "W40A"
        SurChargeString = vbNullString

			rc = SelectDairy (stRow)
			If rc<>micPass Or IsObject(Quick3270)=False Then
				FindSCDiaryandRun = micFail
				Exit Function
			End If
			            
            With Screen
                SurChargeString = .GetString(12, 22, 3)
                SurChargeString = Trim(SurChargeString)
                If SurChargeString = 852 Then
					SCDairy = True

                    rc =ExecuteDairy(diaryString,DCode)
					If rc <> micPass Or IsObject(Quick3270)=False Then 
						FindSCDiaryandRun = micFail
						Exit Function
					End If
					
                    Exit Do
                Else
                    Screen.SendKeys "<Pf3>"

                    rc = StringWait ("Enter", 7, 2, "Waits at diaries list Screen")
					If rc <> micPass Or IsObject(Quick3270)=False Then 
						FindSCDiaryandRun = micFail
						Exit Function
					End If
					
                End If

            End With
            
    SCDairy = False
    stRow = stRow + 1
    Loop
    
    If SCDairy = False Then 
		FindSCDiaryandRun = micFail
    ElseIf SCDairy = True Then 
		FindSCDiaryandRun = micPass
    End If
    
End Function


'################################################################################################################################
'	FUNCTION NAME		: InitializeEmulator()

'	DESCRIPTION			: Private Function which closes the emulator

'	PARAMETERS		   : App --- Application - Valid Values are "APS"
'											  Region -- Valid Values are "IST2","QA","RETRO2"

'	DEPENDENCIES		: Execution machine should have Quick3270 Application installed.

' 	RETURN VALUE(S): 
'		micPass	 		: On successful execution of the function
'		micFail	 		: On unsuccessful execution of the function

'	ERRORS				: (None)

'	AUTHOR				: Ramu Gurrapu

'	ORIGINAL DATE		: Sep 22, 2008
'-----------------------------------------------------------------------------------------------------------------------
'			R E V I S I O N    H I S T O R Y
'-----------------------------------------------------------------------------------------------------------------------
'	REVISED DATE	: 	REVISED BY	 :	CHANGE DESCRIPTION
'-----------------------------------------------------------------------------------------------------------------------

Private Function InitializeEmulator(ByVal App, ByVal Region)
       
    'Identifying the enviorment and assigning appropriate command..
    If UCase(Trim(App)) = "APS" And UCase(Trim(Region)) = "IST2" Then EnvString = "CIT02COG"       'Op-38
    If UCase(Trim(App)) = "APS" And UCase(Trim(Region)) = "RETRO2" Then EnvString = "CIT04COG"     'Op-40
    If UCase(Trim(App)) = "APS" And UCase(Trim(Region)) = "QA" Then EnvString = "CIPQACOG"         'Op-47
        
    ServerIP = "10.140.33.48"
    DRString = "AL22"
    
    'Creating Object for Emulator Screen
    Set Quick3270 = CreateObject("Quick3270.Application")
    Quick3270.Visible = True
    Set Session = Quick3270.ActiveSession
    Set Screen = Session.Screen
                   
    'Connects to the Server
    Session.Server_Name = ServerIP
    Session.Connect

    rc = StringWait("Type", 4, 2,"Current Step : Entered Server IP & Waiting for the Session to Connect")

	If rc<>micPass Or isObject(Quick3270)=False Then
		InitializeEmulator = micFail
	Else
		InitializeEmulator =micPass
	End If
                   
End Function


'################################################################################################################################
'	FUNCTION NAME		: ExitEmulator()

'	DESCRIPTION			: Private Function which closes the emulator

'	PARAMETERS		   : None

'	DEPENDENCIES		: Execution machine should have Quick3270 Application installed.

' 	RETURN VALUE(S): 
'		micPass	 		: On successful execution of the function
'		micFail	 		: On unsuccessful execution of the function

'	ERRORS				: (None)

'	AUTHOR				: Ramu Gurrapu

'	ORIGINAL DATE		: Sep 22, 2008
'-----------------------------------------------------------------------------------------------------------------------
'			R E V I S I O N    H I S T O R Y
'-----------------------------------------------------------------------------------------------------------------------
'	REVISED DATE	: 	REVISED BY	 :	CHANGE DESCRIPTION
'-----------------------------------------------------------------------------------------------------------------------
Private Function ExitEmulator()
    
    If tmpRes = True Then
        With Screen
            tmpRes = .WaitForString("COMMAND", 23, 2)
            .MoveTo 23, 10
            .SendKeys ("end")
            .SendKeys ("<Enter>")
        
            tmpRes = .WaitForString("TERMINAL", 12, 21)
            .SendKeys ("logoff")
            .SendKeys ("<Enter>")
        
            tmpRes = .WaitForString("Type", 4, 2)
            .SendKeys ("logoff")
            .SendKeys ("<Enter>")
        End With
    End If
    
    ServerIP = vbNullString
    
    Quick3270.Visible = False

    Set Screen = Nothing
    Set Session = Nothing
    Set Quick3270 = Nothing
    
    EnvString = vbNullString
    DRString = vbNullString
    tmpRes = vbNullString

	ExitEmulator = micPass
					
End Function


'################################################################################################################################
'	FUNCTION NAME		: StringWait()

'	DESCRIPTION			: Public Function which waits for given string

'	PARAMETERS		   : Strng  -- To be search string
'											: Row - Row in which string needs to search
'											: Col - Col in which string needs to search
'											: ErrMessage - Err Message if String did nt find at given location.

'	DEPENDENCIES		: Execution machine should have Quick3270 Application installed.

' 	RETURN VALUE(S): 
'		micPass	 		: On successful execution of the function
'		micFail	 		: On unsuccessful execution of the function

'	ERRORS				: (None)

'	AUTHOR				: Ramu Gurrapu

'	ORIGINAL DATE		: Sep 22, 2008
'-----------------------------------------------------------------------------------------------------------------------
'			R E V I S I O N    H I S T O R Y
'-----------------------------------------------------------------------------------------------------------------------
'	REVISED DATE	: 	REVISED BY	 :	CHANGE DESCRIPTION
'-----------------------------------------------------------------------------------------------------------------------
Private Function StringWait(ByVal Strng, ByVal Row, ByVal Col, ByVal ErrMessage)

    tmpRes = Screen.WaitForString(Strng, Row, Col)           'Waits for USER ID Screen
    If tmpRes <> True Then
		ExitEmulator
		StringWait = micFail
		eMessage = ErrMessage
	Else
		StringWait = micPass
    End If
    
End Function